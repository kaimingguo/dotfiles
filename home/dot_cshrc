#
# .cshrc - csh resource script, read at beginning of execution by each shell
#
# see also csh(1), environ(7).
# more examples available at /usr/share/examples/csh/
#

if ( -f "$home/.config/csh/init.csh" && -r "$home/.config/csh/init.csh" ) then
	source "$home/.config/csh/init.csh"
endif

# Aliases
if ( -f "$home/.config/csh/aliases.csh" && -r "$home/.config/csh/aliases.csh" ) then
	unalias *
	source "$home/.config/csh/aliases.csh"
endif

# These are normally set through /etc/login.conf.  You may override them here
# if wanted.
# set path = (/sbin /bin /usr/sbin /usr/bin /usr/local/sbin /usr/local/bin $HOME/bin)
# A righteous umask
# umask 22

# Docker
if ( -f "$home/.docker/init-csh.sh" && -r "$home/.docker/init-csh.sh" ) then
	source "$home/.docker/init-csh.sh"
endif

if ( -f "$home/.config/csh/environment.csh" && -r "$home/.config/csh/environment.csh" ) then
	source "$home/.config/csh/environment.csh"
endif

if ($?prompt) then
	# An interactive shell -- set some stuff up
	set prompt = "%N@%m:%~ %# "
	set promptchars = "%#"

	set filec
	set history = 1000
	set savehist = (1000 merge)
	set autolist = ambiguous
	# Use history to aid expansion
	set autoexpand
	set autorehash
	set mail = (/var/mail/$USER)
	if ( $?tcsh ) then
		set pushdsilent
		set pushdtohome

		bindkey "^W" backward-delete-word
		bindkey -k up history-search-backward
		bindkey -k down history-search-forward

		if ( -f "$home/.config/csh/completion.csh" && -r "$home/.config/csh/completion.csh" ) then
			uncomplete *
			source "$home/.config/csh/completion.csh"
		endif
	endif

	if ( $uid == 0 ) then
		unset savehist
	endif

endif

if ( $?ANDROID_HOME ) then
	if ( $?ANDROID_NDK ) then
		if ( -d "$ANDROID_NDK" && "$path" !~ *"$ANDROID_NDK"* ) then
			set path = ( $path $ANDROID_NDK )
		endif
	endif

	foreach _bin ( cmdline-tools/latest/bin platform-tools tools tools/bin )
		if ( -d "$ANDROID_HOME/${_bin}" && "$path" !~ *"$ANDROID_HOME/${_bin}"* ) then
			set path = ( $path $ANDROID_HOME/${_bin} )
		endif
	end
	unset _bin
endif

if ( $?JAVA_HOME ) then
	if ( -d "$JAVA_HOME/bin" && "$path" !~ *"$JAVA_HOME/bin"* ) then
		set path = ( $JAVA_HOME/bin $path )
	endif
endif

if ( $?FLUTTER_HOME ) then
	if ( -d "$FLUTTER_HOME/bin" && "$path" !~ *"$FLUTTER_HOME/bin"* ) then
		set path = ( $FLUTTER_HOME/bin $path )
	endif
endif

if ( $?DOTNET_ROOT ) then
	if ( -d "$DOTNET_ROOT" && "$path" !~ *"$DOTNET_ROOT"* ) then
		set path = ( $DOTNET_ROOT $path )
	endif
endif

if ( -e `where go` ) then
	set _go_bin = ( `go env GOPATH`/bin )
	if ( -d "${_go_bin}" ) mkdir -p ${_go_bin}
	if ( "$path" !~ *"${_go_bin}"* ) set path = ( ${_go_bin} $path )
	unset _go_bin
endif

if ( -e `where python` ) then
	set _base_bin = "`python -m site --user-base`/bin"
	if ( -d "${_base_bin}" && "$path" !~ *"${_base_bin}"* ) then
		set path = ( ${_base_path} $path )
	endif
	unset _base_bin
endif

foreach _tool_bin ( $home/.jbang/bin $home/.yarn/bin $home/.cargo/bin $home/.pub-cache/bin )
	if ( -d "${_tool_bin}" && "$path" !~ *"${_tool_bin}"* ) then
		set path = ( ${_tool_bin} $path )
	endif
end
unset _tool_bin

# Set path so it include user's private bin if it exists
if ( -d "$home/bin" && "$path" !~ *"$home/bin"* ) then
	set path = ( $home/bin $path )
endif

if ( -d "$home/.local/bin" && "$path" !~ *"$home/.local/bin"* ) then
	set path = ( $home/.local/bin $path )
endif

if ( -f "$home/.config/csh/after.csh" && -r "$home/.config/csh/after.csh" ) then
	source "$home/.config/csh/after.csh"
endif

if ( -e `where starship` ) then
	eval `starship init tcsh --print-full-init`
endif

